buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.3'
    }
}


allprojects {
    group = 'org.jvalue.ceps'
    version = '0.1.0-SNAPSHOT'
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.jfrog.artifactory'

    sourceCompatibility = 1.7

    // setup generated sources (if any)
    ext {
        mainSourcesDir = 'src/main/java'
        generatedSourcesDir = 'target/generated-sources/java'
    }
    sourceSets {
        main {
            java {
                srcDir mainSourcesDir
                srcDir generatedSourcesDir
            }
        }
    }

    // default dependency locations
    repositories {
        jcenter()
        mavenLocal()
    }

    // show compiler warnings
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }

    // gradle wrapper
    task wrapper(type: Wrapper) {
        gradleVersion = '2.3'
    }

    // integration test
    def testSrcDir = file('src/integrationtest/java')
    def testResourceDir = file('src/integrationtest/resources')
    sourceSets {
        integrationTest {
            java.srcDir testSrcDir
            resources.srcDir testResourceDir
            compileClasspath = sourceSets.main.output + configurations.testCompile + sourceSets.test.output
            runtimeClasspath = output + compileClasspath
        }
    }
    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
    }
    check.dependsOn integrationTest

    // let intellij know about 'special' folders
    idea {
        module {
            testSourceDirs += testSrcDir
            sourceDirs += file(generatedSourcesDir)
        }
    }

    // snapshots
    artifactory {
        contextUrl = 'http://oss.jfrog.org/artifactory'
        resolve {
            repository {
                repoKey = 'libs-snapshot'
                maven = true
            }
        }
    }
}

